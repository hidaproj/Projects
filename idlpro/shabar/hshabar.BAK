; hshabar.pro
@caiolib2

;**************************************************************
function version
ver='0.0'	; '2011/03/06	k.i. 	opy from nkrprj\idlpro\hardware\caio.pro
ver='0.1'	; '2011/08/20	k.i.
ver='0.2'	; '2011/09/05	k.i.  	recdat, make_dir, csv
ver='0.3'	; '2011/09/11	k.i.  	CAIO Start/Stop, on PC7
ver='0.4'	; '2011/09/14	k.i.  	plot, gif
ver='0.5'	; '2011/09/25	k.i.  	calcpsd,intval=p.rate/2
ver='0.6'	; '2011/09/30	k.i.  	-> C:\command\shabar.txt
ver='0.6'	; '2011/09/30	k.i.  	-> C:\command\shabar.txt
ver='0.7'	; '2012/02/04	k.i.  	output for p.nch > 1, onlyintens[j] and scinti[j]
ver='0.8'	; '2012/02/08   TTI     add ftp in plot
ver='0.9'	; '2013/08/30   M.H.    idl VM
return,ver
end

;**************************************************************
function hshabar_ctl	; return CAIO control parameter structure

c=caio_cardst()

p={hshabar_ctl, $	; hshabar control params.
	dev_exist:	1, $	; device exist(1) not not(0)
	rate:		200, $	; sampling rate [Hz]
	nch:		3, $	; # of sampling channels
	dt_read1: 	0.05, $	; time of every reading for continuous sampling, [sec]
	n_sample: 	100, $	; # of data for one read
	n_rec1: 	10800l, $	; # of data for one record
	trange:		[0.,1.], $	; time interval for monitor [sec]
	yrange:		[0,10.], $	; data display range
	datadir:	'', $	; output data dir
	logdir:		'C:\data\SHABAR\log\', $	; output data dir
	gifdir:		'C:\data\SHABAR\gif\', $	; output data dir
	recdat:		1,  $	; 0 - rec only log,  1 - rec time series dat
	rec:		0, $	; Rec(1) or through (0)
	do1:		0, $	; Digital IO ch1
	dt:		0., $	; wait time, sec
	darklevel:	0., $	; dark level
;	frange:		[10.,200.], $	; freq. range to calc rms, Hz
	frange:		[50.,200.], $	; freq. range to calc rms, Hz test on 120503
	intens:		0., $	; mean intensity
	scinti:		0., $	; rms(I)/I
	date:		'', $	; yymmdd
	time:		'', $	; hhmmss
	version:	version(), $
	c:		c $
	}
return,p
end

;**************************************************************
pro hshabar_event, ev
;--------------------------------------------------------------
common hshabar, wd, p, dat
;  wd  -- widget
;  p   -- control parameters
;  dat -- read data

widget_control, ev.id, get_uvalue=value

case (ev.id) of
  wd.ShbStart: begin ;----------------------------------- SHABAR start -----------
	p=caio_pSet(p)
	ev_shbstop=widget_event(wd.ShbStop,/nowait)
	t=findgen((p.trange(1)-p.trange(0))*p.rate)/p.rate
	nch=p.c.nch &	dn1=p.c.n_evsample
	plot,t,t,xtitle='time [sec]',yrange=p.yrange,ytitle='V',/nodata,ystyle=1
	is=0l	; if 0, initial plot
	ndat=0l	; # of recorded data in dat(*,*)
	tt=findgen(p.n_rec1)/p.rate	; sec
	df=float(p.rate)/p.n_rec1
	caio_start
	p.rec=1
	count=0
	nc=20
	cs=indgen(nc)
	intens=fltarr(p.nch)
	scinti=fltarr(p.nch)
	while ev_shbstop.id eq 0 do begin
		for i=nc-1,1, -1 do cs[i]=cs[i-1]
		cs[0]=count
		caio_wd_status,wd,st=st,count=count,ndat=ndat,DATA_NUM=dnev
		tdif=total(abs(count-cs))
		;print,tdif,count,cs
		if tdif eq 0 then begin
			caio_stop
			dmy=yymmdd(get_time=time)
			print,time,':  CAIO Stop/Start...',count
			wait,0.1
			caio_start
		endif
		ev_shbstop=widget_event(wd.ShbStop,/nowait)
		if dnev eq 1 then begin
			dat1=caio_read(nch,dn1,ret=ret)	; <--- read data
			if ret ne 0 then print,'Caio_read: ',ret
			if ndat eq 0 then dat=dat1 else dat=[[dat],[dat1]]
			ndat=ndat+dn1
			if ndat ge p.n_rec1 then begin
				dmy=yymmdd(get_time=time)
				p.time=strmid(time,0,2)+strmid(time,3,2)+strmid(time,6,2)
				outc=time
				dat=dat-p.darklevel
				for j=0,p.nch-1 do begin
					datq=transpose(dat[j,*])
					calcpsd,tt,datq,f,pw,intval=p.rate/2,over=0.5
					ii=where(f ge p.frange[0] and f lt p.frange[1])
					ipw=total(pw[ii])*df
					irms=sqrt(ipw)
					intens[j]=mean(datq)
					scinti[j]=irms/intens[j]
					outc=outc+', '+string(intens[j],form='(f8.5)')+', '+string(scinti[j],form='(f8.6)')
				endfor
				corr12=correlate(dat[0,*],dat[1,*])
				outc=outc+', '+string(corr12,form='(f8.5)')
				p.intens=intens[0]
				p.scinti=scinti[0]
				widget_control,wd.Intens,set_value=string(p.intens,form='(f7.3)')
				widget_control,wd.Scinti,set_value=string(p.scinti,form='(f8.5)')
				openw,1,p.logdir+'hsb_'+p.date+'.csv',/append
				fs=fstat(1)
				if fs.size eq 0 then begin	; new csv file
					com='    time'
					for j=0,p.nch-1 do begin
						cch=string(j+1,form='(i1)')
						com=com+',  intens'+cch+',  scinti'+cch
					endfor
					com=com+',  corr12'
					printf,1,com
				endif
				printf,1,outc
				close,1
				openw,2,'C:\command\shabar.txt'
				printf,2,outc
				close,2
				ndat=0l
				if p.recdat then begin
					outfile=p.DataDir+'hsb_'+p.time+'.sav'
					print,'dat --> ',outfile
					save,p,dat,file=outfile
				endif
				;help,dat
			endif
			im=is*dn1
			if im+dn1 gt n_elements(t) then begin
				is=0l & im=0l
				plot,t,t,xtitle='time [sec]',yrange=p.yrange,ytitle='V',/nodata,ystyle=1
			endif
			if im gt 0 then begin
				t1=t[im-1:im+dn1-1]
				datp=[[dat_1],[dat1]]
			endif else begin
				t1=t[im:im+dn1-1]
				datp=dat1
			endelse
			for j=nch-1,0,-1 do oplot,t1,datp[j,*],color=254-j
			wait,0.001	; <== see
			is=is+1
			dat_1=dat1[*,dn1-1]
		endif
	endwhile
	caio_stop
	p.rec=0
	help,dat
	caio_wd_status,wd,st=st,count=count,ndat=ndat,DATA_NUM=dnev
	end

  wd.Recdat: begin
  	p.recdat = ev.value
  	if p.recdat then make_dir,p.datadir
	end

  wd.AdStart: begin ;----------------------------------- monitoring -----------
	p=caio_pSet(p)
	ev_adstop=widget_event(wd.AdStop,/nowait)
	ev_recstart=widget_event(wd.RecStart,/nowait)
	ev_recstop=widget_event(wd.RecStop,/nowait)
	ev_rec1=widget_event(wd.Rec1,/nowait)
	if ev_recstart.id ne 0 then p.rec=2
	t=findgen((p.trange(1)-p.trange(0))*p.rate)/p.rate
	nch=p.c.nch &	dn1=p.c.n_evsample
	plot,t,t,xtitle='time [sec]',yrange=p.yrange,ytitle='V',/nodata,ystyle=1
	is=0l	; if 0, initial plot
	ndat=0l	; # of recorded data in dat(*,*)
	caio_start
	while ev_adstop.id eq 0 do begin
		caio_wd_status,wd,st=st,count=count,ndat=ndat,DATA_NUM=dnev
		if p.rec eq 0 then begin
			ev_recstart=widget_event(wd.RecStart,/nowait)
			ev_rec1=widget_event(wd.Rec1,/nowait)
			ev_nrec1=widget_event(wd.NRec1,/nowait)
		endif else ev_recstop=widget_event(wd.RecStop,/nowait)
		if ev_nrec1.id ne 0 then begin
			p.n_rec1=long(gt_wdtxt(wd.NRec1))
			print,p.n_rec1
		endif
		if ev_recstart.id ne 0 then begin
			p.rec=2
			ndat=0l
			ev_recstart.id=0
			print,'REC start...'
		endif
		if ev_rec1.id ne 0 then begin
			p.rec=1
			ndat=0l
			ev_rec1.id=0
			print,'REC1 start...'
		endif
		if ev_recstop.id ne 0 then begin
			p.rec=0
			ev_recstop.id=0
			print,'REC stop...'
		endif
		if dnev eq 1 then begin
			dat1=caio_read(nch,dn1,ret=ret)	; <--- read data
			if ret ne 0 then print,'Caio_read: ',ret
			if p.rec ne 0 then begin
				if ndat eq 0 then dat=dat1 else dat=[[dat],[dat1]]
				ndat=ndat+dn1
				if p.rec eq 1 and ndat ge p.n_rec1 then begin
					p.rec=0
					print,'REC1 finish..'
					help,dat
				endif
			endif
			im=is*dn1
			if im+dn1 gt n_elements(t) then begin
				is=0l & im=0l
				plot,t,t,xtitle='time [sec]',yrange=p.yrange,ytitle='V',/nodata,ystyle=1
			endif
			if im gt 0 then begin
				t1=t(im-1:im+dn1-1)
				datp=[[dat_1],[dat1]]
			endif else begin
				t1=t(im:im+dn1-1)
				datp=dat1
			endelse
			;for j=0,nch-1 do oplot,t1,datp(j,*),color=254-j
			for j=nch-1,0,-1 do oplot,t1,datp(j,*),color=254-j
			is=is+1
			dat_1=dat1(*,dn1-1)
		endif
		ev_adstop=widget_event(wd.AdStop,/nowait)
		;if keyword_set(wd_stop2) then ev_stop2=widget_event(wd_stop2,/nowait)
		;----- branch
		ev_emp=widget_event(/nowait)
		;if ev_emp.id ne 0 then print,ev_emp.id
		;-----
	endwhile
	caio_stop
	p.rec=0
	help,dat
	caio_wd_status,wd,st=st,count=count,ndat=ndat,DATA_NUM=dnev
	end
  wd.GetStat: begin
	caio_wd_status,wd,st=st,count=count,DATA_NUM=dnev
	p.c=caio_getinfo()
	help,p.c,/st
	end
  wd.AdStop: begin
	caio_stop
	end
  wd.ShbStop: begin
	caio_stop
	end
  wd.RecStart: begin
	end
  wd.RecStop: begin
	end
  wd.Rec1: begin
	end
  wd.NRec1: begin
	p.n_rec1=long(gt_wdtxt(ev.id))
	end
  wd.DO1: begin
        case ev.value of
            0: begin
                caio_dout,0,0
                p.do1=0
                end
            1: begin
                caio_dout,0,1
                p.do1=1
               end
        endcase
	end
  wd.Rate: begin
	p.rate=fix(gt_wdtxt(ev.id))
	end
  wd.Dt: begin
	p.dt=fix(gt_wdtxt(ev.id))
	end
  wd.Trange: begin	; time range for monitor
	p.trange[1]=float(gt_wdtxt(ev.id))
	print,p.trange[1]
	end
  wd.Vrange: begin	; V range for monitor
	p.yrange[1]=float(gt_wdtxt(ev.id))
	end
  wd.Nch: begin
	p.nch=float(gt_wdtxt(ev.id))
	end
  wd.Save: begin
	file=dialog_pickfile(path=p.datadir,filter='*.isv',title='CAIO data',get_path=path)
	p.datadir=path
	save,p,dat,file=file
	print,'p, dat saved in '+file
	end
  wd.Plot: begin
  	csvfile=p.logdir+'hsb_'+p.date+'.csv'
	device,decom=1
  	hsb_plot, csvfile, wid=2
	device,decom=0
	;@set_color
	;  set_color.pro
	loadct,0,/silent
	stretch,255,0
	red=254 &	tvlct,255,0,0,red
	green=253 &	tvlct,0,200,0,green
	blue=252 &	tvlct,0,0,255,blue
	lightblue=251 &	tvlct,0,255,255,lightblue
	purple=250 &	tvlct,255,0,255,purple
	black=249 &	tvlct,0,0,0,black
	yellow=248 &	tvlct,255,255,0,yellow
	gray=247 &	tvlct,100,100,100,gray
	grayr=246 &	tvlct,150,100,100,grayr
	grayg=245 &	tvlct,100,150,100,grayg
	orange=244 &	tvlct,255,200,100,orange

	colnames=['red','green','blue','lightblue','purple','black', $
		'yellow','gray','grayr','grayg','orange']

	giffile=p.gifdir+'hsb_'+p.date+'.gif'
  	win2gif,giffile
  	wait,0.5
  	openr,1,giffile
  	sstmp=fstat(1)
	tmpgif=bytarr(sstmp.size)
	readu,1,tmpgif
	close,1
  	tmp_ret=call_external('C:\Projects\IDLPRO\shabar\ftptest.dll','PutData',tmpgif,sstmp.size, $
  						'192.168.0.210','/smart3/shabar/gif/hsb_'+p.date+'.gif','smart','spw!3BHa',/cdecl)
  	print,tmp_ret
	openr,1,csvfile
  	sstmp=fstat(1)
	tmplog=bytarr(sstmp.size)
	readu,1,tmplog
	close,1
  	tmp_ret=call_external('C:\Projects\IDLPRO\shabar\ftptest.dll','PutData',tmplog,sstmp.size, $
  						'192.168.0.210','/smart3/shabar/log/hsb_'+p.date+'.csv','smart','spw!3BHa',/cdecl)
	print,tmp_ret
	end
  wd.Gif: begin
  	giffile=p.gifdir+'hsb_'+p.date+'.gif'
  	win2gif,giffile
	end
  wd.Exit: begin
	openr,1,'C:\log\sts20'+p.date+'.txt'
  	sstmp=fstat(1)
	tmplog=bytarr(sstmp.size)
	readu,1,tmplog
	close,1
  	tmp_ret=call_external('C:\Projects\IDLPRO\shabar\ftptest.dll','PutData',tmplog,sstmp.size, $
  						'192.168.0.210','/work/syslog/sts20'+p.date+'.txt','smart','spw!3BHa',/cdecl)
	print,tmp_ret
	openr,1,'C:\log\com20'+p.date+'.txt'
  	sstmp=fstat(1)
	tmplog=bytarr(sstmp.size)
	readu,1,tmplog
	close,1
  	tmp_ret=call_external('C:\Projects\IDLPRO\shabar\ftptest.dll','PutData',tmplog,sstmp.size, $
  						'192.168.0.210','/work/syslog/com20'+p.date+'.txt','smart','spw!3BHa',/cdecl)
	print,tmp_ret
	wdelete
	WIDGET_CONTROL, /destroy, ev.top
	;caio_exit
	end
  else:
endcase

end


;**************************************************************
pro make_dir,dir
oscomdll='C:\Projects\cprog\VS2005\oscom32\Debug\oscom32.dll'
d=file_search(dir)
if d[0] eq '' then dmy=call_external(oscomdll,'Dmkdirs',dir)

end

;**************************************************************
function widget_hshabar,base,p	; display widget status
;--------------------------------------------------------------

wd_hsb={wd_hsb,	$
	Status:		0l,	$
	Count:		0l,	$
	Ndat:		0l,	$
	Memsld:		0l,	$
	ShbStart:	0l,	$
	ShbStop:	0l,	$
	AdStart:	0l,	$
	AdStop:		0l,	$
	RecStart:	0l,	$
	RecStop:	0l,	$
	Rec1:		0l,	$
	NRec1:		0l,	$
	Getstat:	0l,	$
	DO1:		0l,	$
	Rate:		0l,	$
	Trange:		0l,	$
	Vrange:		0l,	$
	Nch:		0l,	$
	DataDir:	0l,	$
	Dt:			0l,	$
	Intens:		0l,	$
	Scinti:		0l,	$
	Recdat:		0l,	$
	Plot:		0l,	$
	Gif:		0l,	$
	Save:		0l,	$
	Exit:		0l	$
	}

st=caio_status(BUSY=busy,START_TRG=START_TRG,DATA_NUM=data_num,OFERR=oferr)
count=caio_count()
sldmax=p.c.maxn/p.c.nch

lab = widget_label(base,value='>>> H-SHABAR: '+p.c.card+'   <<<');,font=2)

	base1=widget_base(base, /column)
	b_1=widget_base(base1, /row, /frame)
	lab = widget_label(b_1,value='Status:')
	;wd_hsb.Status = widget_text(b_1,value=string(st,form='(z8.8)')+'H',xsize=9)
	wd_hsb.Status = widget_label(b_1,value=string(st,form='(z8.8)')+'H',xsize=70)
	lab = widget_label(b_1,value='Mamory:')
	;wd_hsb.Count = widget_text(b_2,value=string(count,form='(i6.6)'),xsize=6)
	wd_hsb.Count = widget_label(b_1,value=string(count,form='(i6.6)'),xsize=50)
;	wd_hsb.Memsld  = widget_slider(b_2, value=0, uvalue='Csld', $
;		minimum=0, maximum=sldmax, xsize=150,suppress=1, vertical=0, frame=40)
	lab = widget_label(b_1,value=' data#:')
	;wd_hsb.Ndat = widget_text(b_1,value=string(0,form='(i6.6)'),xsize=6)
	wd_hsb.Ndat = widget_label(b_1,value=string(0,form='(i6.6)'),xsize=40)

	b_2=widget_base(base1, /column, /frame)
	b_2a=widget_base(b_2, /row)
	lab = widget_label(b_2a,value='DataDir:')
	wd_hsb.DataDir=widget_text(b_2a,value=string(p.datadir, form='(a30)'), $
		xsize=30, uvalue='DataDir',/edit)
	if p.recdat then make_dir,p.datadir
	b_2b=widget_base(b_2, /row)
	b_2c=widget_base(b_2, /row)
	lab = widget_label(b_2b,value='Shb:')
	wd_hsb.ShbStart = widget_button(b_2b, value="Start", uvalue = "ShbStart")
	wd_hsb.ShbStop = widget_button(b_2b, value="Stop", uvalue = "ShbStop")
	lab = widget_label(b_2b,value=' I:')
	wd_hsb.Intens=widget_text(b_2b,value='', xsize=10, uvalue='Intens')
	lab = widget_label(b_2b,value=' dI/I:')
	wd_hsb.Scinti=widget_text(b_2b,value='', xsize=10, uvalue='Scinti')
;	lab = widget_label(b_2b,value='  dt:')
;	wd_hsb.Dt=widget_text(b_2b,value=string(p.dt, form='(i3)'), xsize=6, uvalue='Dt',/edit)
	lab = widget_label(b_2c,value='Rec.Dat[*]:')
	wd_hsb.Recdat = cw_bgroup(b_2c,['0','1'],/row, uvalue="Recdat", $
		/no_release,set_value=p.recdat,/exclusive,/frame)
	wd_hsb.Plot = widget_button(b_2c, value="Plot", uvalue = "Plot")
	wd_hsb.Gif = widget_button(b_2c, value="Gif", uvalue = "Gif")

if 0 then begin
	b_3=widget_base(base1, /row, /frame)
	lab = widget_label(b_3,value='AD:')
	wd_hsb.AdStart = widget_button(b_3, value="Start", uvalue = "AdStart")
	wd_hsb.AdStop = widget_button(b_3, value="Stop", uvalue = "AdStop")
	lab = widget_label(b_3,value='  Rec:')
	wd_hsb.RecStart = widget_button(b_3, value="Start", uvalue = "RecStart")
	wd_hsb.RecStop = widget_button(b_3, value="Stop", uvalue = "RecStop")
	lab = widget_label(b_3,value=' ')
	wd_hsb.Rec1 = widget_button(b_3, value="Rec1", uvalue = "Rec1")
	wd_hsb.Save = widget_button(b_3, value="Save", uvalue = "Save")
endif

	bb=widget_base(base1, /column, /frame)
;	b_31=widget_base(bb, /row, ysize=25)
;	lab = widget_label(b_31,value='DO1:')
;	wd_hsb.DO1 = cw_bgroup(b_31,['0','1'],/row, uvalue="DO1", $
;		/no_release,set_value=0,/exclusive,/frame)
;	wd_hsb.GetStat = widget_button(b_31, value="Status", uvalue = "GetStat")

	b_4=widget_base(bb, /row)
	lab = widget_label(b_4,value='Rate:')
	wd_hsb.Rate=widget_text(b_4,value=string(p.rate, form='(i5)'), xsize=6, uvalue='Rate',/edit)
	lab = widget_label(b_4,value='Hz,  Nch:')
	wd_hsb.Nch=widget_text(b_4,value=string(p.nch, form='(i2)'), xsize=3, uvalue='Nch',/edit)
	lab = widget_label(b_4,value=',  NRec:')
	wd_hsb.NRec1=widget_text(b_4,value=string(p.n_rec1, form='(i8)'), xsize=8, uvalue='NRec1',/edit)
	lab = widget_label(b_4,value='')
	b_5=widget_base(bb, /row)
	lab = widget_label(b_5,value='tmax:')
	wd_hsb.Trange=widget_text(b_5,value=string(p.trange(1), form='(i3)'), xsize=3, uvalue='Tragge',/edit)
	lab = widget_label(b_5,value='s')
	lab = widget_label(b_5,value=',  Vmax:')
	wd_hsb.Vrange=widget_text(b_5,value=string(p.yrange(1), form='(i3)'), xsize=3, uvalue='Tragge',/edit)
	lab = widget_label(b_5,value='V')

;	wd_hsb.Exit = widget_button(b_4, value="Exit", uvalue = "Exit")
;	b_5=widget_base(bb, /row)
;	wd_hsb.DatDir=widget_button(b_5, value="Data dir:", uvalue = "DatDir")
;	lab = widget_label(b_5,value=p.datadir,font=2)

	return,wd_hsb

end

;************************************************************************
pro hshabar

common hshabar, wd, p, dat
; wd	- widget structure
; p	- control parameters
;caiodll='C:\Projects\cprog\VS2005\ContecAIOW2\Debug\ContecAIOW2.dll'
caiodll='C:\Projects\cprog\VS2005\ADI12_8\Debug\ADI12_8.dll'

p=hshabar_ctl()
caio_init,p=p,st=st,dllfile=caiodll
help,p.c,/st
p.yrange=[-1,10]
p.trange=[0,5]
p.rate=5000	; sampling, Hz
p.n_rec1=10000l	; length of one record
p.nch=2
p.datadir='c:\data\shabar\20'+yymmdd()+'\'
p.date=yymmdd()
p.darklevel=0.
p.recdat=0


window,0,xsize=600,ysize=400
;@set_color
;  set_color.pro
loadct,0,/silent
stretch,255,0
red=254 &	tvlct,255,0,0,red
green=253 &	tvlct,0,200,0,green
blue=252 &	tvlct,0,0,255,blue
lightblue=251 &	tvlct,0,255,255,lightblue
purple=250 &	tvlct,255,0,255,purple
black=249 &	tvlct,0,0,0,black
yellow=248 &	tvlct,255,255,0,yellow
gray=247 &	tvlct,100,100,100,gray
grayr=246 &	tvlct,150,100,100,grayr
grayg=245 &	tvlct,100,150,100,grayg
orange=244 &	tvlct,255,200,100,orange

colnames=['red','green','blue','lightblue','purple','black', $
	'yellow','gray','grayr','grayg','orange']
;==============
device,decom=0
t=findgen((p.trange(1)-p.trange(0))*p.rate)/p.rate
plot,t,t,xtitle='time [sec]',yrange=p.yrange,ytitle='V',/nodata,ystyle=1


base = WIDGET_BASE(title='H-SHABAR:  Ver.'+version(), /column)
wd = widget_hshabar(base,p)
wd.Exit=widget_button(base, value="Exit", uvalue = "Exit")
widget_control, base, /realize
XMANAGER, 'hshabar', base


end

